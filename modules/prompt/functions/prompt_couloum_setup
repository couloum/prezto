#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Florian Coulmier <florian@coulmier.fr>
#
# Screenshots:
#

function prompt_couloum_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_couloum_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_couloum_precmd
  
  if [ "$USER" = "root" ]; then
    HOSTNAMECOLOR="red"
  else
    HOSTNAMECOLOR="magenta"
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "%F{cyan}%b%f"
  zstyle ':prezto:module:git:info:added' format "%F{green}+%f"
  zstyle ':prezto:module:git:info:deleted' format "%F{yellow}⨉%f"
  zstyle ':prezto:module:git:info:modified' format "%F{red}?%f"
  zstyle ':prezto:module:git:info:untracked' format "%F{magenta}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%f(%b%d%a%m%u) '

  # Set editor parameters
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{green}>%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{red}>%f'

  # Define prompts.
  #PROMPT="%F{blue}<%F{cyan}%T%F{blue}>[%F{$HOSTNAMECOLOR}%m%F{blue}:%F{yellow}%4~%F{blue}]"'${git_info:+${(e)git_info[prompt]}}''${editor_info[keymap]}'"%f%# "
  PROMPT="%F{$HOSTNAMECOLOR}%m%F{blue}"'${editor_info[keymap]}'"%F{yellow}%3~%f%# "
  #RPROMPT='%F{blue}${ruby_info[version]}'
  RPROMPT="%(?..%F{red}»%? %f)"'${git_info:+${(e)git_info[prompt]}}'"%F{blue}[%F{cyan}%T%F{blue}]"
}

prompt_couloum_setup "$@"
